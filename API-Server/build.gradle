plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'

    //restdocs-api-spec → restdocs extension  포함함
    id 'com.epages.restdocs-api-spec' version '0.17.1'
    //swagger generator 플러그인 추가
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'swm.hkcc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
jar.enabled = false // ..-plain.jar 파일 생성 안함

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'


    // Querydsl 추가
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Spring Rest Docs
    // asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    //restdocs-api-spec 의존성 추가
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.17.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // sentry
    implementation 'io.sentry:sentry-spring-boot-starter-jakarta:6.25.0'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation group: 'it.ozimov', name: 'embedded-redis', version: '0.7.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Querydsl 설정부
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [ generated ]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
}


// Spring Rest Docs

test {
    useJUnitPlatform()
//    outputs.dir snippetsDir
}

// Task 및 설정 추가
// GenerateSwaggerUI 태스크가, openapi3 task 를 의존하도록 설정
tasks.withType(GenerateSwaggerUI) {
    dependsOn 'openapi3'

    //기존 파일 삭제했다가, build 에 출력한 json 정적파일 복사 (안해도 됨 → local 확인용)
    copy {
        from "build/resources/main/static/docs"
        into "src/main/resources/static/docs/"
    }
}


//openAPI 작성 자동화를 위해 패키징 전에 openapi3 테스크 선실행을 유발
bootJar{
    dependsOn(':openapi3')
}

openapi3 {
    server = "https://dev.swm-lgtm.com"
    title = "LGTM API Docs - Dev"
    description = "LGTM의 Dev Server API 문서입니다."
    version = "0.0.1"
    outputFileNamePrefix = 'open-api-3.0.1'
    format = 'json'

    // /static/docs/open-api-3.0.1.json 생성 → jar 파일만 배포할 예정이기에 build 에 출력
    outputDirectory = 'build/resources/main/static/docs'
}
